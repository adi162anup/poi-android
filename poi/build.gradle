apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java' // Needed for shadow to work
apply plugin: 'maven-publish'
assemble.dependsOn shadowJar

dependencies {
    implementation "org.apache.poi:poi-ooxml:$poiVersion"
    implementation 'org.apache.xmlbeans:xmlbeans:2.6.0'
    implementation 'com.fasterxml:aalto-xml:1.2.2'
}

shadowJar {
    archiveName 'poi-all.jar'

    // exclude 'schemaorg_apache_xmlbeans/attribute/**'
    // exclude 'schemaorg_apache_xmlbeans/attributegroup/**'
    // exclude 'schemaorg_apache_xmlbeans/element/**'
    // exclude 'schemaorg_apache_xmlbeans/identityconstraint/**'
    // exclude 'schemaorg_apache_xmlbeans/javaname/**'
    // exclude 'schemaorg_apache_xmlbeans/modelgroup/**'
    // exclude 'schemaorg_apache_xmlbeans/namespace/**'
    // exclude 'schemaorg_apache_xmlbeans/src/**'
    // exclude 'schemaorg_apache_xmlbeans/system/s8*/**'
    // exclude 'schemaorg_apache_xmlbeans/system/sXML*/**'
    // exclude 'schemaorg_apache_xmlbeans/type/**'

    mergeServiceFiles {
        // merging relocates some files, so exclude these as we want to keep them in place
        exclude 'META-INF/services/javax.xml.stream.*'
    }

    exclude 'repackage/**'

    exclude 'LICENSE.txt'
    exclude 'NOTICE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/maven/**'

    // these are only needed when handling Visio files, remove it if you would like to use Visio support
    dependencies {
        exclude(dependency('com.github.virtuald:curvesapi'))
        exclude(dependency('commons-codec:commons-codec'))
    }

    // Relocate javax dependencies so Android does not choke
    relocate 'javax.xml.namespace', 'org.apache.poi.javax.xml.namespace'
    relocate 'javax.xml.stream', 'org.apache.poi.javax.xml.stream'
    relocate 'javax.xml.XMLConstants', 'org.apache.poi.javax.xml.XMLConstants'

    // java.awt is not available, but class Color is used in APIs in POI, therefore
    // relocate this class to another one where we can include a rewrite
    relocate 'java.awt.Color', 'org.apache.poi.java.awt.Color'
    relocate 'java.awt.geom.Dimension2D', 'org.apache.poi.java.awt.geom.Dimension2D'
    relocate 'java.awt.Dimension', 'org.apache.poi.java.awt.Dimension'
    relocate 'java.awt.image.BufferedImage', 'org.apache.poi.java.awt.image.BufferedImage'

    relocate 'javax.imageio.ImageIO', 'org.apache.poi.javax.imageio.ImageIO'
    relocate 'javax.imageio.ImageReader', 'org.apache.poi.javax.imageio.ImageReader'
    relocate 'javax.imageio.stream.ImageInputStream', 'org.apache.poi.javax.imageio.stream.ImageInputStream'
    relocate 'javax.imageio.metadata.IIOMetadata', 'org.apache.poi.javax.imageio.metadata.IIOMetadata'
}

artifacts {
    compile shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.supercilex.poiandroid'
            artifactId 'poi'
            version poiVersion

            artifact file("$buildDir/libs/poi-all.jar")

            pom.withXml {
                asNode().with {
                    appendNode('packaging', 'jar')
                    appendNode('name', "POI Android")
                    appendNode('description', 'Apache POI for Android')
                }
            }
        }
    }
}
